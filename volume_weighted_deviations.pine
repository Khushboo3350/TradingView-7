//@version=4
study("Volume Weighted Deviations, by ", overlay=true)

ls = input(false, title="Log-space")
type = input(title="Type", defval="Average Deviation", options=["Standard Deviation", "Average Deviation"])
length = input(50)
source = input(close)
_low = ls == true?log(low):low
_high = ls == true?log(high):high
src = ls == true?log(source):source

//weighted mean
pine_vwmean(x, y) =>
    cw = 0
    cd = 0.0
    w_sum = 0.0
    d_sum = 0.0
    for i = 0 to y -1
        cd = x[i]
        cw = volume[i]
        d_sum := d_sum + cw * cd
        w_sum := w_sum + cw
    d_sum/w_sum

//weighted standard deviation
pine_vwstdev(x, y, b) =>
    d_sum = 0.0
    w_sum = 0.0
    cd = 0.0
    for i = 0 to y - 1
        cd = x[i]
        cw = volume[i]
        d_sum := d_sum + cw*pow(cd - b, 2)
        w_sum := w_sum + cw
    sqrt(d_sum/w_sum)

//weighted average deviation
pine_vwavdev(x, y, b) =>
    d_sum = 0.0
    w_sum = 0.0
    cd = 0.0
    for i = 0 to y - 1
        cd = x[i]
        cw = volume[i]
        d_sum := d_sum + cw*abs(cd - b)
        w_sum := w_sum + cw
    d_sum/w_sum

vwmean = pine_vwmean(src, length)

//consider using Average Deviation instead of Standard Deviatio if there are values outside of 3rd upper & lower bands within a rolling window
dev = if type == "Standard Deviation" 
    dev = pine_vwstdev(src, length, vwmean)
else if type == "Average Deviation"
    dev = pine_vwavdev(src, length, vwmean)
    
plot(ls == true?exp(vwmean - dev*3):vwmean - dev*3, color=#787B86, title="Lower dev 3")
plot(ls == true?exp(vwmean - dev*2):vwmean - dev*2, color=#2196F3, title="Lower dev 2")
plot(ls == true?exp(vwmean - dev):vwmean - dev, color=#2196F3, title="Lower dev 1")
plot(ls == true?exp(vwmean):vwmean, color=#363A45, title="Basis")
plot(ls == true?exp(vwmean + dev):vwmean + dev, color=#2196F3, title="Upper dev 1")
plot(ls == true?exp(vwmean + dev*2):vwmean + dev*2, color=#2196F3, title="Upper dev 2")
plot(ls == true?exp(vwmean + dev*3):vwmean + dev*3, color=#787B86, title="Upper dev 3")
